plugins {
    id 'java'
    id("com.gradleup.shadow") version "9.0.0-beta7"
    id 'de.eldoria.plugin-yml.paper' version '0.7.0'
    id 'xyz.jpenilla.run-paper' version "2.3.1"
}

group = 'dev.slne.surf.serverbrandcustomizer'
version = '1.21.4-1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven { url = "https://repo.codemc.io/repository/maven-releases/" }
    maven { url = "https://repo.codemc.io/repository/maven-snapshots/" }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT'
    compileOnly 'com.github.retrooper:packetevents-spigot:2.7.0'
    compileOnly 'io.netty:netty-all:4.1.117.Final'
}

paper {
    main = 'dev.slne.surf.serverbrandcustomizer.SurfServerbrandCustomizer'
    apiVersion = '1.21'
    authors = ['twisti']
    description = 'Customize the server brand displayed to the player in the F3 menu'

    serverDependencies {
        'packetevents' {
            required = true
            load = 'BEFORE'
        }
    }
}

tasks {
    runServer {
        minecraftVersion('1.21.4')
        downloadPlugins {
            modrinth('packetevents', '2.7.0')
            modrinth('viaversion', '5.2.2-SNAPSHOT+673')
        }
    }

    shadowJar {
        archiveClassifier = ''
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}